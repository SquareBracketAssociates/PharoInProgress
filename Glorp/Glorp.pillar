! Object-Relational Persistence with Glorp

This document decribes the Glorp object-relational mapper. By reading it, you will learn how to map your objects to relational database. This document will revisit the relational database concepts. Then its tutorial will present step by step how to use Glorp and its advanced concepts.
We thank the author of the previous Glorp tutorials R. Withney and N. Pratt to have granted us the right to use some of their material. This document has been written by E. Maringolo and edited by S. Ducasse.

!! What is Glorp

Working in a live object environment such Pharo is great. You can freely create your domain objects and link or compose them as you like. Sometimes those objects can be stored in a way that preserves the original design    but sometimes you have to store your objects in a Relational Database Management System (a.k.a. ''RDBMS'') which requires you to flatten your object graph into related tables managed by the RDBMS.

Such process of mapping object to tables is called ''Object-Relational Mapping'' (''ORM''), and like it or not, it imposes certain constraints in the design of you object model to support its persistence in tables. Some models are easier to map into tables than others, the difficulty lies in what is known as ''*Object-Relational Impedance Mismatch>https://en.wikipedia.org/wiki/Object-relational_impedance_mismatch*''.

To work with relational databases, Pharo provides a battle-tested ORM created at CampSmalltalk a decade ago and maintained since then, its name is Glorp for ''Generic Lightweight Object-Relational Persistence''. It is usually called both GLORP (all caps, as an accronym), or Glorp (as a proper name).

Glorp is an fully featured ORM which provides you with a handful of features to reduce the ''impedance'' as much as possible. Among those features you'll find it saves you from writing SQL queries by hand, managing    transactions that rollback the changes to the objects in you image or commit them to the database, writing simple and complex queries using plain Smalltalk syntax, and other features that we will cover in this introduction chapter and in chapter of advanced topics.


!! Installation

!!! Database server
Before installing Glorp you should already have installed the RDBMS of your choice in your machine or a reacheable server, it could be *PostgreSQL (http://postgresql.org)>http://postgresql.org*, *MySQL (http://dev.mysql.com/)>http://dev.mysql.com/*, *SQLite (http://sqlite.org)>http://sqlite.org*, or any other of your preference (as long it is supported). Later in the Concepts Section Glorp we will refer to this RDBMS as the ''Platform''.

!!! Database Drivers
Along with your working installation of your RDBMS of choice you should have installed in your image the drivers required to connect to the database server.
- Native drivers (written in Pharo, no external dependencies)
--    PostgreSQL
--    MySQL

- UDBC
--    PostgreSQL
--    MySQL
--    SQLite (UFFI)
--    ODBC (UFFI, Windows only)
--    OpenDBX (UFFI)

@@todo What is UDBC?

!!! Glorp Packages

Glorp comes in a few packages you can load using a conventional ''Metacello''
expression. One package is the ==Glorp-Core== and there is also a
==Glorp-Tests== package.

[[[
Metacello new
        project: 'Glorp';
        load
]]]

!!! Running the Tests

Having loaded the Database drivers and the Glorp packages, it is recommended to
run the unit tests of Glorp, to ensure everything was loaded correctly and it
is working properly.

!! Person: Our First Example Class
To put some of the concepts described before into practicewe will create
a ==Person== class and store it into a ==PERSON== database table,
everything from within the Pharo environment.

!!! Class definition
[[[caption=Defining our example class
Object subclass: #Person
	instanceVariableNames: 'id firstName lastName birthDate'
	classVariableNames: ''
	package: 'Glorp-Book'
]]]

!!!Instance methods
[[[caption=Accessors and initializers
Person >> firstName: aString
  firstName := aString

Person >> firstName
  ^firstName

Person >> lastName: aString
  lastName := aString

Person >> lastName
  ^lastName

Person >> birthDate: aDate
  birthDate := aDate

Person >> birthDate
  ^birthDate

Person >> initialize
  super initialize.
  birthDate := '1/1/1970' asDate
]]]

!!! Defining a DescriptorSystem
As you can see the above created class and methods, doesn't have anything
related with its persistence and doesn't require you to inherit from a
particular class.

Glorp models all the involved concepts (such tables, columns, classes, etc.)
as first class objects, and then links
instances of those objects in the ==DescriptorSystem==.
It is the core of a Glorp system, it holds all the Glorp metadata,
such the Tables, Descriptors and Class Models.

By using a separate artifact (in this case, a class) to define all
the metadata of your system you can decouple your business models
from its persistence information.
This separation of concerns is a good practice,
and helps with the maintenance of your code.

Also, having an orthogonal description of your domain objects allows you
to have more than one descriptor for the same business domain class,
this is an important difference with patterns such as ''ActiveRecord''
where the persistence metadata is defined in the business domain class,
and you can't reuse the same class for two different
systems using different persistence configurations.

All the definitions and mappings are defined in a sublclass of
==DescriptorSystem==, and for that we will create our own subclass.

[[[caption=Our descriptor system class
DescriptorSystem subclass: #GlorpBookDescriptorSystem
	instanceVariableNames: ''
	classVariableNames: ''
	package: 'Glorp-Book'
]]]

We said before that Glorp has a whole metamodel that involves describing the
mapped class, the table(s) where it is going to be mapped and the mapping
itself. To define each one of those, Glorp follows a convention in
the method naming, we will mention the conventions below.

!!!! Class Model
We start by describing the class ==Person==, and the way to
do it is by defining a method with the pattern ==classModelFor''YourClass'':==.

[[[caption=Describing the class model for the Person class.
GlorpBookDescriptorSystem >> classModelForPerson: aClassModel
    aClassModel newAttributeNamed: #id.
    aClassModel newAttributeNamed: #fistName.
    aClassModel newAttributeNamed: #lastName.
    aClassModel newAttributeNamed: #birthDate.
]]]

Note that a class model is a Glorp representation of your domain class. Glorp uses it to
store metadata about your domain objects. The descriptor system provides hooks to let you define
such class model. ==classModelForPerson:== is one of such hooks. Based on the name convention enonced
before, Glorp associates automatically one class model and to your domain class.

!!!! Table Model
The class ==Person== will be stored in a single table (which is the usual case), and we
provide the description of the table in a similar way as with the class
model, i.e., by following the ==tableFor''YOURTABLE''== convention.
Please notice the upper case of the table name.

[[[caption=Table model for PERSON table.
GlorpBookDescriptorSystem >> tableForPERSON: aTable
    (aTable createFieldNamed: 'id' type: platform serial) bePrimaryKey.
    aTable createFieldNamed: 'firstName' type: (platform varChar: 100).
    aTable createFieldNamed: 'lastName' type: (platform varChar: 100).
    aTable createFieldNamed: 'birthDate' type: platform date.
]]]

The ''serial'' datatype, explained in detail below, is an autoincrementing
integer datatype, everytime you save a new instance Glorp will obtain a
new integer and assign it to your instance.

!!!! Descriptor (aka ''Mapping'').
Once we have the class model and the table object, we should define the mappings
between the class attributes and the table fields.
In this simple example we use a ==DirectMapping==, which is a class of
mapping that takes the value in the column and assigns it to the attribute,
and vice versa.

[[[caption=Defining the mapping between the class and the table.
GlorpBookDescriptorSystem >> descriptorForPerson: aDescriptor
    | table |
    table := self tableNamed: 'PERSON'.
    aDescriptor table: table.
    (aDescriptor newMapping: DirectMapping)
        from: #id
        to: (table fieldNamed: 'id').
    (aDescriptor newMapping: DirectMapping)
        from: #firstName
        to: (table fieldNamed: 'firstName').
    (aDescriptor newMapping: DirectMapping)
            from: #lastName
            to: (table fieldNamed: 'lastName').
    (aDescriptor newMapping: DirectMapping)
        from: #birthDate
        to: (table fieldNamed: 'birthDate')
]]]

In the above method we can see how the the descriptor ''links'' the class
attribute to the field name.

Although verbose and maybe not visible in this simple example, here lies the
power of the Glorp orthogonal description system, you describe everything
and then link all the parts the way you want without modifying your domain
object.

!!! Creating the Tables

Assuming we haven't created the database tables externally, Glorp's metamodel
allows you to perform DDL (''Data Definition Language'') commands such as
==CREATE TABLE== or ==CREATE CONSTRAINT== (among others) using plain
Pharo objects, and it even can determine when to run those.

To do that we must first connect to the database, and we will explain how to it in the following pages.
We're using a PostgreSQL server running in the same host as our Pharo image. We will then create the Login object and a  ==DatabaseAccessor== for it to interact with the database.

!!! Database Accessor
Glorp is programmed to be agnostic of the Smalltalk dialect and
database driver, to achieve that instead of talking directly to the
driver it uses an adapter object that acts as an intermediary between
Glorp and the underlying driver, each database driver requires its
own subclass of ==DatabaseAccessor==.

We use the recommended accessor, which is the ''UDBC'' accessor
that is compatible with all the drivers supported by
the ''Unified Database Connectivity'' library.

@@todo Stef: did we load this library? where?
@@todo EAM: Not yet, it will depend if by the end of the book the library is ready to be used

@@todo Stef: I do not get it: each driver requires its own subclass now what is a PharoDatabaseAccessor use later?
@@todo EAM: The PharoDatabaseAccessor name is misleading, it assumes (from VisualWorks) that you only have one unified driver API to access the database, which is not so at the moment. The accessor is an adapter that Glorp talks to, and this adapter talks to the database driver, so if you have different DB APIs, then you need different accessors.

To define it as the default driver we must evaluate once the following expression:

[[[caption=Setting the default Driver.
"Run this once"
UDBCGlorpAccessor beDefaultGlorpDriver
]]]

!!! The Login
All the information necessary to connect to the database is defined in an instance of the
==Login== class, it is an abstraction for the connection parameters
used to connect to the database server, like the ''Plaform'' used,
hostname, port, username, password, database name and other parameters.

[[[caption=Creating a login.
login := Login new
        database: PostgreSQLPlatform new;
        username: 'postgres';
        password: 'secret';
        connectString: 'localhost_glorpbook'.
]]]

The accessor will require a ==Login== that will use to establish a connection
to the database server specified in the login, using also the platform specified in it.

[[[
accessor := UDBCDatabaseAccessor forLogin: login.
accessor login.
]]]

!!!!! Verifying the connectivity.
If everything goes fine, then evaluating ==accessor isLoggedIn== should answer
''true''. Another way to test it is performing a basic query like the following
==(accessor basicExecuteSQLString: 'SELECT 3+4') contents first first== should
return ''7'' as the result; and yes, the ==contents first first== is dirty,
but it is not mean to be used this way, we're doing it just to be sure.

!!! Session

Once we're able to connect to the database we can start interacting with it using
Glorp's objects. We will do that using an object that ''orchestrates''
 all the other objects, the ==GlorpSession==. The easiest way to get a
full blown session is to ask the ==DescriptionSystem== passing the a ==Login==
instance as argument.

[[[caption=Accessing a session.
session := GlorpBookDescriptorSystem sessionForLogin: login.
session login.
]]]

!!! How the Parts Play Together

Let us look into the implementation of the method ==DescriptiorSystem>>sessionForLogin:== to understand
better how each part interacts with each other.

[[[caption=GlorpBookDescriptorSystem inherits this implementation.
DescriptorSystem class >> sessionForLogin: aGlorpLogin
	| system session |
	system := self forPlatform: aGlorpLogin database.
	session := GlorpSession new.
	session accessor: (DatabaseAccessor forLogin: aGlorpLogin).
	session system: system.
	^session
]]]

First we get a ''system'' instantiating the receiver (in our case
 ==GlorpBookDescriptorSystem==) passing the platform as argument.
The platform in the ''system'' is important because it will be used to
generate ''SQL'' code specific for it, using particular syntax and data types.

Then we instantiate a ==GlorpSession== and store it in the ''session'' object.
This ''session'' object is assigned a ==DatabaseAccessor==, it will use such
accessor to communicate to the database, hence the name, but to know what to
send to the ''db'' it needs all the metadata defined in the ''system'' object
defined before.

Note we ask the abstract ==DatabaseAccessor== class to return a concrete
instance for ''aGlorpLogin'', it will return an instance of the
database accessor configured as default.

!!!!! A note about the ''Platform''.
In Glorp terminology a Platform is the RDBMS (for ''Relational Database
Management System'') platform. The abstract class ==Platform== defines the
abstract methods to deal with differences of supported data types,
test the support of certain features, and so on.

!!! Saving/Updating/Deleting Objects
It is time to put to work all the classes and schema we just created. To do it
we will create and persist a few instances of our example class ==Person==, and
then read some of them back. We'll explain a little bit
how Glorp decides what and when to ''INSERT'', ''UPDATE'' or ''DELETE'' an object using
the concept of Unit of Work. But first we should create tables.

!!! Creating Tables
Now we have our working ''session'' object, we can perform our ''DDL'' queries
extracting the data from the ''system'' and sending commands to
the ''accessor''. But before we should evaluate the following to create the tables.

[[[
session createTables
]]]

And ''voilÃ '', it will send the required commands to the database to create our tables,
sequences, constraints and so on.

!!! Transactions and ''Unit of Work''
If you worked with RDBMS's before, you know you can use transactions that
detach you from immediate writes to the database files, and enable you to
rollback your changes if it is necessary, or do a batch of changes and commit
them all at once.

Glorp provides you with ''automatic'' handling of such database transactions,
but also adds another concept, the ''Unit of work'' (aka ''UOW'' or ''UnitOfWork'').
This ''UnitOfWork'' keeps track of all the persisted objects, enabling
you to commit or rollback changes ""at the object level"" while also maintaining
the database transaction in sync with it.

!!! Saving New Instances

To save instances we should send the message ==register: anInstance== to a session.
Note that this operation should occur within a Unit of Work using the message
==inUnitOfWork:== as follows:

[[[caption=Saving example instances of Person.
session inUnitOfWorkDo: [
    {
        (Person new firstName: 'John'; lastName: 'Locke'; birthDate: '1704-08-29' asDate).
        (Person new firstName: 'John'; lastName: 'Malkovich'; birthDate: '1953-12-09' asDate).
        (Person new firstName: 'George'; lastName: 'Lucas'; birthDate: '1944-05-14' asDate)
    } do: [ :each | session register: each ]
].
]]]

As you can see, we didn't say if the instance needs to be inserted or updated,
nor we specify an ''id'' for each instance. The framework will determine that
by ''registering'' the instance in the all-mighty ''session''.

!!! Reading Instances

We can now read back some of the persisted instances, for that we, again, use
the session object, sending it the message ==read:== and the class as argument.
This will return a collection of results.

[[[caption=Read all instances of Person|language=smalltalk
session read: Person
]]]

Of course you don't always read all the instances from the database but
want to filter the results. For that you can pass a block that will act as
a filter.

[[[caption=Read all instances of Person matching a criteria|language=smalltalk
session read: Person where: [:each | each firstName = 'John'].
]]]

This might lead you to think you can pass any block to filter the results
as you can do with ==select:== in a regular Collection, but that is not possible.
Glorp uses blocks as a very clever way to build expressions that
are later converted to SQL by the framework, you can do basic filtering
using this feature, but keep in mind it doesn't work as a regular block.

Besides the use of regular operators such =====, ==>===, etc
One of examples of the mentioned above is the use of ''special'' selectors
that get translated to their counterparts SQL operators.
For instance the ==similarTo:== selector gets converted to SQL's ==LIKE==
operator. With this we can retrieve all the Persons named ''John''.

[[[caption=Use an operator that gets ''translated''|language=smalltalk
session read: Person where: [:each | each lastName similarTo: 'Malk%'].
]]]

Another convenient way to retrieve a single instance instead of a Collection
is to use the ==readOneOf:== or ==readOneOf:where:== message in an analogous
way to ==read:== and ==read:where:==.

!!! Update of Instances

We can also update one instance, i.e., to read one and update it. We should do it
in the context of a UnitOfWork using the message ==inUnitOfWorkDo:==:

[[[caption=Updating an object read within a UnitOfWork
session inUnitOfWorkDo: [
    | person |
    person := session readOneOf: Person where: [:each | each lastName = 'Locke'].
    person birthDate: Date today.
    ].
]]]

Note that to update an object accessed from the database does not require you to
send the message ==register:== to the session object,
as we did while populating the table.

This is because when you're working inside a Unit of Work all the objects
you read are automatically registered by the session, saving you from explicitly
''registering'' them (which is safe to do anyway, but it is not required).

!!! Deletion of Objects
So far we covered Creation, Read, and Update, we're only missing the Deletion of
objects. This is as easy as it gets, just send the ==delete:== to the session
object with the object you want to delete as argument.

[[[caption=Deleting an object inside of a UnitOfWork
session inUnitOfWorkDo: [
    | person |
    person := session readOneOf: Person where: [:each | each lastName = 'Locke'].
    session delete: person
    ].
]]]

Another way to delete objects is passing a condition block.

[[[caption=Delete using a where clause
session inUnitOfWorkDo: [
    session delete: Person where: [:each | each lastName = 'Locke'].
    ].
]]]

You can delete an object without being inside of a UnitOfWork, this will
cause the object to be deleted right away, without giving you the option
to rollback such action unless you handle the transaction manually.

!!! Transactions
Glorp lets you either handle automatically transactions using unit of work or
manually as we show now.

!!!! Rolling Back Changes
*Atomic>https://en.wikipedia.org/wiki/Atomicity_%28database_systems%29*
modifications to a database are important, and to achieve that you need transactions
 that let you rollback changes if something fails.

A Unit of Work handles the errors and other unexpected terminations of
the block, so if something fails, it not only rollbacks any changes at the
database level if necessary, but also, and more importantly
it reverts the changes in your objects.

The following example shows that the last name of the Locke object will not be modified
and the database updated since the unit of work failed.

[[[caption=Updating an object read within a UnitOfWork
session inUnitOfWorkDo: [
	person := session readOneOf: Person where: [:each | each lastName = 'Locke'].
	person lastName: 'Wayne'.
	Error signal: 'This error will abort the Unit Of Work.'
].
]]]

!!!! Managing the Transaction Manually
The ==inUnitOfWorkDo:== message is a convenient way to isolate a block of execution
within the context of a transaction at both object and database levels. However
it has some limitations, like not being able to handle ''nested'' UnitOfWorks
or transactions, everything happens in the context of the outer context transaction
in which it was evaluated.

If for some reason you need to handle the start/end/rollback of the
unit of work manually, you can do so by using the following messages:

-==beginUnitOfWork==
-==commitUnitOfWork==
-==rollbackUnitOfWork==
-==commitUnitOfWorkAndContinue==

The first three have self descriptive selectors, and can be using like this:

[[[caption=Manual handling of the transaction
person := session readOneOf: Person where: [:each | each firstName = 'George'].
session beginUnitOfWork.
session delete: person.
session rollbackUnitOfWork.
session beginUnitOfWork.
person lastName: 'Lukas'.
session register: person.
session commitUnitOfWork.
]]]

In the case of the message ==commitUnitOfWorkAndContinue== it needs some explanation,
but the concept is simple: It commits the current unit of work, and then
creates a new one migrating all the objects registered in the commited unit of
work to the newly created, and still open, unit of work. If this paragraph
confuses you, looking at its implementation might explain it better.

It is useful for cases like batch loads or updates,
where you want to commit changes every ''n'' instances or similar.

[[[caption=Commiting and continuing the Unit Of Work
session beginUnitOfWork.
10 to: 99 do: [ :index |
     session register: (
		Person new
			firstName: 'Sample';
			lastName: index printString).
	(index \\ 10) isZero ifTrue: [
		session commitUnitOfWorkAndContinue
	].
].
session commitUnitOfWork.
]]]

We can cleanup the sample instances by running:
[[[caption=Delete using a where clause
session inUnitOfWorkDo: [
    session delete: Person where: [:each | each firstName = 'Sample'].
].
]]]


!! Glorp Configurations and Additional Concepts

In our previous example we created a simple class that mapped ''1:1'' with a
table using simple data types, but Glorp provides many more features than
an ''ActiveRecord'' like mapping. Tt lets you fine tune the persistence of
your classes. We will go over the different configurations of class models,
table data types and constraints and mappings of all sorts.

!!! Class Models
A class model defines the attributes of your domain objects, and how they compose.
Each of your persisted objects need to have an instance of ==GlorpClassModel==
in the descriptor system containing a collection of all the attributes of
your class, attributes that by convention are added by means of
implementing ==classModelForYourClass:==,

The simplest way to add an attribute is using a series of convenience methods,
described below:

;==newAttributeNamed: #email==
:Used to define simple scalar/literal values such as Numbers, Strings, Dates, etc.
;==newAttributeNamed: #address type: Address==
:Used to define 1:1 (one to one) relations with other objects of your domain model.
;==newAttributeNamed: #invoices collectionOf: Invoice==
:Used to define 1:n (one to many) and n:m (many to many) relations of your class model with other models.
;==newAttributeNamed: #invoices collection: collectionClass of: Invoice==
:Similar as the one above for 1:n and n:m relations, but you can define what kind of Collection is going to be used.
;==newAttributeNamed: #counters dictionaryFrom: keyClass to: valueClass==
:Used to define an attribute that is a Dictionary where the key is ==keyClass== and its values are instances of ==valueClass==.

!!! Attribute Properties
The above described methods return instances of ==GlorpAttributeModel==, which share common properties that you can configure.

;==useDirectAccess: aBoolean==
:Let you define whether the access to the attribute described by the symbol of your domain model will be performed by directly accessing the instance variable (slot) instead of using a standard message send.
;==beForPseudoVariable==
:Useful for cases where you want to describe an attribute that won't be read nor written, but still described to be used on queries.

[[[caption=Example class model for our Person class
GlorpBookDescriptorSystem >> classModelForPerson: aClassModel
  (aClassModel newAttributeNamed: #id) useDirectAccess: true.
  aClassModel newAttributeNamed: #firstName.
  aClassModel newAttributeNamed: #lastName.
  aClassModel newAttributeNamed: #birthDate.
  aClassModel newAttributeNamed: #email.
  aClassModel newAttributeNamed: #address type: Address.
  aClassModel newAttributeNamed: #invoices collectionOf: Invoice.
  aClassModel newAttributeNamed: #counters from: String to: Integer.
]]]

!!! Table Models
Glorp also models your database objects, such as tables, constraints, indexes,
etc.. With this model it will be able to determine how to serialize to SQL
the objects, how to perform joins to retrieve 1:1 or 1:n relations, and so on.

The descriptor system follows a convention to define the tables,
it uses the ==tableForTABLENAME:== selector to configure ==TABLENAME==, the
argument of this method is an instance of ==DatabaseTable==, and this
method is responsible for describing your table in the relational
database, including field names and their data types, contraints (primary keys
and/or foreign keys), etc.

!!!! Adding Fields to the Table
Let's bring back our example from the beginning:

[[[caption=Table model for PERSON table
GlorpBookDescriptorSystem >> tableForPERSON: aTable
  (aTable createFieldNamed: 'id' type: platform serial) bePrimaryKey.
  aTable createFieldNamed: 'firstName' type: (platform varChar: 100).
  aTable createFieldNamed: 'lastName' type: (platform varChar: 100).
  aTable createFieldNamed: 'birthDate' type: platform date.
]]]

As you can see, you can add a field by sending ==createFieldNamed:type:== to
the table object. The first argument of the method is name of field
(aka ''column'') in your table, the second one is the datatype.
For the datatype we're not specifying any particular implementation of the type
but instead we send a message to the ''platform'' object, and in Glorp jargon
the platform is the RDBMS we'll be using.

Doing it this way enables our table model to be RDBMS agnostic, and, for instance,
it will work with SQLite or PostgreSQL (or any other platform).
E.g. if our platform is PostgreSQL then ==platform varchar== will return
==VARCHAR==, but if instead our platform is SQLite then it will return ==TEXT==
because SQLite only supports ==TEXT== as datatype for character based fields.

Going back to ==createFieldNamed:type:==, it will return an instance of
==DatabaseField==, which has properties of its own, such as if the field is
''nullable'', ''unique'', its default value, and some convenience methods such
as ==bePrimaryKey== which will create a ''PK'' (primary key) on the table
for this field.

Commonly used datatypes in alphabetic order

||selector||''SQL-92'' datatype||Pharo class
|{ blob |{ BLOB |{ ByteArray
|{ boolean |{ BOOLEAN |{ Boolean
|{ date |{ DATE |{ Date
|{ decimal |{ NUMERIC |{ ScaledDecimal
|{ double |{ DOUBLE |{ Float
|{ float |{ REAL |{ Float
|{ integer |{ INTEGER |{ Integer
|{ serial |{ SERIAL |{ Integer
|{ time |{ TIME |{ Time
|{ timestamp |{ TIMESTAMP |{ DateAndTime
|{ varchar |{ VARCHAR |{ String

You can find more datatypes by browsing the ''types'' method category of
==DatabasePlatform== or any of its subclasses, not all databases support all
datatypes, and some platforms have their own datatype. If you decide to use
a datatype that's only supported by a particular RDBMS you will lose the
benefits of being platform agnostic, so it is a tradeoff.

!!! Mappings (aka ''Descriptor'')
Once we have the Class Model describing our business objects and the
Table Models describing the tables where they will be stored, we need to
describe how they are going to work together; to achieve that Glorp uses
an instance of ==Descriptor== that contains the mappings between the attributes
of the class model and the fields of the tables involved.

As expected, there is a convention to define Descriptors and, you guessed right,
it is ==descriptorFor''YourClass'':== that will receive as an argument a
instance of ==Descriptor== on which you will configure the mappings.

[[[caption=Example descriptor for our Person class.
GlorpBookDescriptorSystem >> descriptorForPerson: aDescriptor
  | table |
  table := self tableNamed: 'PERSON'.
  (aDescriptor newMapping: DirectMapping) from: #id to: (table fieldNamed: 'id').
  (aDescriptor newMapping: DirectMapping) from: #firstName to: (table fieldNamed: 'firstname').
  "snip..."
  (aDescriptor newMapping: OneToOneMapping) attributeName: #address.
  (aDescriptor newMapping: ToManyMapping) attributeName: #invoices.
]]]

As you can see, the usual practice is to get a reference to the tables involved
(one in this example, could be more), and then add ''mappings'' to
==aDescriptor== by sending it the message ==newMapping:== with an instance of
the class of mapping we want to use.

There are many mappings that Glorp provides, but the most used ones are:

!!!! Direct mapping.
To map a number, a string, a date or any other ''value'' like object
you use an instance of ==DirectMapping==, specifying which attribute from
your class model you want to map to a field of a table.

Although many times they will be the same symbol, the argument you pass to
the ==from:== parameter is not the symbol of the selector
but instead the name of attribute you have defined in your ==classModelFor...:==
method.

!!!! 1:1 Relationships.
When you're trying to map a single reference from one object to another,
you use the class ==OneToOneMapping== and specify the name of the attribute
that this is instance is mapping. If everything is as simple as this you don't
have to specify further instructions and Glorp will determine how to retrieve
the instances based on the information provided in the ClassModel, foreign keys,
etc. If you need further instructions you can always define them, more on that later.

!!!! 1:N Relationships.
If you are trying to map a collection that belongs to a ''parent/owner'' object
like the invoices of a Person or the items of such invoice to the invoice itself,
then you have to use the ==OneToManyMapping== class.
It is commonly used when you have the rows of the tables at the ''N side'' of
the relation have a field that ''points back'' to the the ''1 side''.
In our example, the invoices have a Foreign Key pointing back to the persons table.

!!!! N:M Relationships.
The Many to Many relationships is similar to the previous one, but in this case
the other side of the relation might ''belong'' to more than one owner.
For instance, if you have Person and Tag, a Person might have many Tags,
but these tags in turn belong to other instances of Person.
In this case you use the ==ManyToManyMapping== and also have to specify
the ''attributeName''.

From your the Pharo point of view, you will continue to see a regular collection
but when storing its elements it will require a third table to do it, called
a ''link table'', Glorp has a convention to name such link tables, and it is
based on composing the names of both sides of the relations.
Following our Person to Tag relation, the table name Glorp will expect is
==TAG_ON_PERSON==, and this must also have its definition in the descriptor system,
so in this case there must be a ==tableForTAG_ON_PERSON:== method.

!!! Common Properties of Relationship Mappings

All ==OneToOneMapping==, ==OneToManyMapping== and ==ManyToManyMapping== are
convenience subclasses of ==RelationshipMapping==, and as such they share
common properties that can be modified.

For instance, the only difference between ==OneToManyMapping== and
==ManyToManyMapping== is that the later is initialized to use a link table,
but you can have a ==OneToManyMapping== that uses a link table if you prefer,
you simple send ==useLinkTable== to it.

!!!!Reference Classes.
All ==RelationshipMapping== instances can specify the class they reference
by sending ==referenceClass:== with the desired class as argument. This is
used a lot, and is a convenience method that ends up modifying the attribute
in the ClassModel. So you can specify it at either place you find more
convenient.

Note we only define the class referenced, because the other side of the
relationship is the class for which we're defining the mapping itself.

Other attribute common to all ==RelationshipMapping== subclasses is that
you can specify how it is suppose to join with other tables, it is,
its ==Join== object. More on this on the advanced section.

!!!!Exclusivity.
Another attribute is the exclusivity of relationships, enabled by sending
==beExclusive== to the mapping. This handy method makes that once the ''parent''
(or ''left'') side of the relation is deleted from the database, Glorp will
manually delete the ''child'' (or ''right'') side of it.
In our Person example, if the relation with ==Address== was exclusive,
then deleting a Person would cause Glorp to delete its address as well.

One caveat of this convenience approach, is that it doesn't play along with database
foreign key actions like ==ON DELETE CASCADE==, basically because one will
happen without the other knowing it, and Glorp will try to delete an
''exclusive'' object that was already removed from the database
by the ''CASCADE'' and because of that it will fail.

!!!! Collection Type.
In the case of ''to many'' mappings you can specify the collection type
of the mapping, for instance, you can send ==collectionType: Set== to your
mapping and have it initialized to a Set instead of the default ==OrderedCollection==.

!!!! Proxy.
And by last, but not least, you can specify whether to proxy or not such relation.
By sending ==shouldProxy:== either ==true== or ==false==, Glorp will
place proxies or immediately build an instance from the database.
If you don't specify this, it will proxy by default.

!!!!! A Word about Proxies.
To avoid reading the whole object graph every time you read an object that
has any kind of relationship mapping, Glorp places a Proxy object instead
of reading it from the database.
This saves a lot of roundtrips to the database, bandwidth and CPU/memory
in the image building objects that you may not ever use.

However, there are cases when you know that certain objects reference
other objects that you know that always will be used, if that is
the case you can avoid the proxying, and instead force the immediate
materialization.

Once Glorp turns a proxy into one of your domain objects, it will store
your object in the Session Cache (more on this later), so future references
to the same object won't cause an additional read from the database, and instead
use the instance in the cache.

And also, Proxies are tricky animals, so beware when debugging something
that involves them. Sometimes they can fool the development tools, making
them believe they're something they're not!

!! Extending our Basic Example
Now that we explained in more detail the main parts of Glorp's descriptor system,
we're ready to extend our basic example, we're going to create a minimal
invoicing model to include the concepts just learnt.

!!! Domain Classes

To do so we will create the following classes ==Person==,
==Address==, ==Invoice==, ==InvoiceItem==, all inheriting from
==GlorpBookObject==, as a convenience to factor the ''id'' attribute.

We will omit the listing of accessor methods, but you should create them.
Regarding the ''id'' attribute, Glorp doesn't require you to use it as
the primary key of your objects.

You can use as many attributes as you want, but for the sake of simplicity we
will use it as a *''surrogate key''>https://en.wikipedia.org/wiki/Surrogate_key*,
which is a key that is a unique identifier an object not derived from
application data.

[[[caption=Hierarchy Root of Book Classes.
Object subclass: #GlorpBookObject
	instanceVariableNames: 'id'
	classVariableNames: ''
	package: 'Glorp-Book'.
]]]

[[[caption=Person Class.
GlorpBookObject subclass: #Person
	instanceVariableNames: 'firstName lastName birthDate addresses invoices tags'
	classVariableNames: ''
	package: 'Glorp-Book'.
]]]

[[[caption=Address Class.
GlorpBookObject subclass: #Address
	instanceVariableNames: 'street number city zip'
	classVariableNames: ''
	package: 'Glorp-Book'.
]]]

[[[caption=Invoice Class.
GlorpBookObject subclass: #Invoice
    	instanceVariableNames: 'issueDate person address items'
    	classVariableNames: ''
    	package: 'Glorp-Book'.
]]]

[[[caption=Invoice Item Class.
GlorpBookObject subclass: #InvoiceItem
    	instanceVariableNames: 'invoice description price'
    	classVariableNames: ''
    	package: 'Glorp-Book'.
]]]

Aside from the accessors, when you're dealing with collections
(to-many relations) that are going to persisted, it is recommended to
early initialize all the instance variables that will reference such collections.
Otherwise it could cause some failures, plus... having a ''complete''
instance is a good practice too.

[[[caption=Initialization methods
Person >> initialize
    super initialize.
    addresses := OrderedCollection new.
    invoices := OrderedCollection new.
]]]

[[[caption=Initialization methods
Invoice >> initialize
    super initialize.
    items := OrderedCollection new.
]]]

!!! Class Model Declarations
Let's create our ==GlorpBookDescriptorSystem== to describe the new
domain model.

[[[caption=Class Model Declarations
GlorpBookDescriptorSystem >> classModelForPerson: aClassModel
    (aClassModel newAttributeNamed: #id) useDirectAccess: true.
    aClassModel newAttributeNamed: #firstName.
    aClassModel newAttributeNamed: #lastName.
    aClassModel newAttributeNamed: #email.
    aClassModel newAttributeNamed: #addresses collectionOf: Address.
    aClassModel newAttributeNamed: #invoices collectionOf: Invoice.
]]]

[[[
GlorpBookDescriptorSystem >> classModelForAddress: aClassModel
    (aClassModel newAttributeNamed: #id) useDirectAccess: true.
    aClassModel newAttributeNamed: #street.
    aClassModel newAttributeNamed: #zip
]]]

[[[
GlorpBookDescriptorSystem >> classModelForInvoice: aClassModel
    (aClassModel newAttributeNamed: #id) useDirectAccess: true.
    aClassModel newAttributeNamed: #issueDate.
    aClassModel newAttributeNamed: #person type: Person.
    aClassModel newAttributeNamed: #address type: Address.
    aClassModel newAttributeNamed: #items collectionOf: InvoiceItem
]]]

[[[
GlorpBookDescriptorSystem >> classModelForInvoiceItem: aClassModel
    (aClassModel newAttributeNamed: #id) useDirectAccess: true.
    aClassModel newAttributeNamed: #invoice type: Invoice.
    aClassModel newAttributeNamed: #description.
    aClassModel newAttributeNamed: #price
]]]


!!!! Table Declarations

Then we should declare the tables that represent our domain.

[[[caption=Table models for Person
GlorpBookDescriptorSystem >> tableForPERSON: aTable
 	(aTable createFieldNamed: 'id' type: platform serial) bePrimaryKey.
	aTable createFieldNamed: 'firstName' type: (platform varChar: 100).
    aTable createFieldNamed: 'lastName' type: (platform varChar: 100).
    aTable createFieldNamed: 'email' type: (platform varchar: 200).
]]]

[[[
GlorpBookDescriptorSystem >> tableForADDRESS: aTable
 	(aTable createFieldNamed: 'id' type: platform serial) bePrimaryKey.
	aTable createFieldNamed: 'street' type: (platform varChar: 100).
    aTable createFieldNamed: 'zip' type: platform integer.
]]]

[[[
GlorpBookDescriptorSystem >> tableForINVOICE: aTable
    | personField addressField |
    (aTable createFieldNamed: 'id' type: platform serial) bePrimaryKey.
    aTable createFieldNamed: 'issueDate' type: platform date.
    personField := aTable createFieldNamed: 'person_id' type: platform integer.
    addressField := aTable createFieldNamed: 'address_id' type: platform integer.
    aTable
        addForeignKeyFrom: personField
        to: ((self tableNamed: 'PERSON') fieldNamed: 'id').
    aTable
        addForeignKeyFrom: addressField
        to: ((self tableNamed: 'ADDRESS') fieldNamed: 'id').
]]]

As you can see in our ''INVOICE'' table, we not only added fields, but also
added ''Foreign Keys'' (aka ''FK''),
to our table table model, this will add the ''FK'' to the list of constraints
of the table model, and will be used by Glorp to order writes/deletions and
infer relations.

Also notice that we need to pass the referenced field as argument,
and to do obtain it we ask the descriptor system for the table of ''PERSON''
and ''ADDRESS'' by sending ==tableNamed:== with the name of table we want
as argument, this will return the ==DatabaseTable== with that name,
and to this object we will ask for the field named ''id''.

[[[
GlorpBookDescriptorSystem >> tableForINVOICEITEM: aTable
    | invoiceField |
    (aTable createFieldNamed: 'id' type: platform serial) bePrimaryKey.
    invoiceField := aTable createFieldNamed: 'invoice_id' type: platform serial.
    aTable createFieldNamed: 'description' type: (platform varchar: 150).
    aTable createFieldNamed: 'price' type: platform decimal.
    aTable createFieldNamed: 'position' type: platform integer.
    aTable
        addForeignKeyFrom: invoiceField
        to: ((self tableNamed: 'INVOICE') fieldNamed: 'id').
]]]


[[[
GlorpBookDescriptorSystem >> tableForPERSON_ON_ADDRESS: aTable
    | personField addressField |
	personField := aTable createFieldNamed: 'person_id' type: platform integer.
    addressField := aTable createFieldNamed: 'address_id' type: platform integer.

	personField bePrimaryKey.
	addressField bePrimaryKey.

    aTable
        addForeignKeyFrom: personField
        to: ((self tableNamed: 'PERSON') fieldNamed: 'id').
    aTable
        addForeignKeyFrom: addressField
        to: ((self tableNamed: 'ADDRESS') fieldNamed: 'id').
]]]

As you can see in the last method, we created a table model for the table
==PERSON_ON_ADDRESS==, this table will be used as a link table. Because
this link table references to other tables we also created Foreign Keys
from each reference field using the same approach described before.

!!! Mapping Declarations

The mapping for the class and table address is simple since there is a direct
mapping between the class model (the domain model) and the table.

[[[caption=Class descriptors
GlorpBookDescriptorSystem >> descriptorForAddress: aDescriptor
    | table |
    table := self tableNamed: 'ADDRESS'.
    aDescriptor table: table.
    (aDescriptor newMapping: DirectMapping)
        from: #id
        to: (table fieldNamed: 'id').
    (aDescriptor newMapping: DirectMapping)
        from: #street
        to: (table fieldNamed: 'street').
    (aDescriptor newMapping: DirectMapping)
            from: #city
            to: (table fieldNamed: 'zip').
]]]


The mapping for invoice is a bit more elaborated, because it not only
contains Direct Mappings, and one-to-one mappings for the ==Person== and
==Address==, but also contains a one-to-many relationship to its items.

We introduced the ==orderBy:== and ==writeTheOrderField== attributes in the
#items mappings, you can use the first attribute independently or both together.

The ==orderBy:== attribute instructs
Glorp to read the elements from the database and ''order'' (aka ''sort'') them
by the field described in the block, this is useful so the collection that holds
the references to the items (in this example) is always sorted the same way,
because the order the rows come from the database is not always the same,
and you can't trust they will return in the same order as they were written.

The ==writeTheOrderField== command will make Glorp save the index of the
element in the collection as a field in the row, it will use the field defined
in ==orderBy:== as its target, in this case ''position''. So if you have an
OrderedCollection with five items, the first element one will have ==1== stored
in the ''position'' field, the second ==2==, and so on. If you change the order
of the elements in the collection, the ''position'' field will be saved with the
new index.

[[[
GlorpBookDescriptorSystem >> descriptorForInvoice: aDescriptor
  | table |
  table := self tableNamed: 'INVOICE'.
  aDescriptor table: table.
  (aDescriptor newMapping: DirectMapping)
    from: #id
    to: (table fieldNamed: 'id').
  (aDescriptor newMapping: DirectMapping)
    from: #issueDate
    to: (table fieldNamed: 'issueDate').
  (aDescriptor newMapping: OneToOneMapping)
    attributeName: #person.
  (aDescriptor newMapping: OneToOneMapping)
    attributeName: #address.
  (aDescriptor newMapping: ToManyMapping)
    attributeName: #items;
    orderBy: [:each | (each getTable: 'INVOICEITEM') getField: 'position'];
    writeTheOrderField.
]]]

The mapping for each invoice item is slightly different since an item is always
part of an Invoice, and only that invoice; so because of that we have a
==OneToOneMapping== referencing its invoice. This way we make the relation
between Invoice and InvoiceItem bidirectional.

[[[
GlorpBookDescriptorSystem >> descriptorForInvoiceItem: aDescriptor
    | table |
    table := self tableNamed: 'INVOICEITEM'.
    aDescriptor table: table.
    (aDescriptor newMapping: DirectMapping)
        from: #id
        to: (table fieldNamed: 'id').
    (aDescriptor newMapping: DirectMapping)
        from: #description
        to: (table fieldNamed: 'description').
    (aDescriptor newMapping: DirectMapping)
        from: #price
        to: (table fieldNamed: 'price').
    (aDescriptor newMapping: OneToOneMapping)
        attributeName: #invoice.
]]]


The mapping for the class Person is more elaborated and worth some explanation
of what happens in the last lines.

[[[caption=Class descriptors
GlorpBookDescriptorSystem >> descriptorForPerson: aDescriptor
  | table linkTable |
  table := self tableNamed: 'PERSON'.
  aDescriptor table: table.
  (aDescriptor newMapping: DirectMapping)
    from: #id
    to: (table fieldNamed: 'id').
  (aDescriptor newMapping: DirectMapping)
    from: #firstName
    to: (table fieldNamed: 'firstName').
  (aDescriptor newMapping: DirectMapping)
    from: #lastName
    to: (table fieldNamed: 'lastName').
  (aDescriptor newMapping: DirectMapping)
    from: #email
    to: (table fieldNamed: 'email').

  (aDescriptor newMapping: ToManyMapping)
    attributeName: #invoices;
    orderBy: [:each | (each getTable: 'INVOICE') getField: 'issueDate'].

  linkTable := self tableNamed: 'PERSON_ON_ADDRESS'.
  (aDescriptor newMapping: ManyToManyMapping)
    attributeName: #addresses;
    referenceClass: Address;
    beExclusive;
    join: (Join
      from: (table fieldNamed: 'id')
      to: (linkTable fieldNamed: 'person_id')).
]]]

Here we introduce the explicit ==join:== attribute of the Reference Mapping.
Usually Glorp infers and creates such Join by looking at the Foreign Keys
of the described or referenced class, but you can make that Join explicit,
you just have to create a ==Join== object with the source field and the
referenced field as arguments. In this case the join is pretty straightforward
and between two fields, but it could be between as many fields as you want.


!!! Sample data

We will create some sample data for our example, and for the Perons we will
save three instances named after famous Smalltalkers, with random addresses,
so don't mail them.

[[[
session inUnitOfWorkDo: [
	| lastnames firstnames |
	{ 'Dan'. 'Alan'. 'Adele' } with: {'Ingalls'. 'Kay'. 'Goldberg'} do:
    [:firstName :lastName |
		| person |
		person := (Person new firstName: firstName; lastName: lastName).
		person addresses add: (
			Address new
				street: (1 to: 1000) atRandom printString, ' Random Avenue';
				zip: (1000 to: 9000 by: 100) atRandom;
				yourself).
		session register: person
	]
].
]]]

Now we created the sample Persons, let's add an extra address for Alan.

[[[caption=Adding Alan's VPRI institute address
session inUnitOfWorkDo: [
	| alan |
	alan := session readOneOf: Person where: [ :each | each firstName = 'Alan' ].
	alan addresses
		add: (Address new
					street: '1025 Westwood Blv, 2nd Floor, Los Angeles, CA';
					zip: 90024;
					yourself)
].
]]]

As you can see, we didn't have to register the newly created address because
the Person was read inside of a UnitOfWork; so if we read the instance again
and inspect its addresses we will find the new instance is there.

[[[caption=Inspect the result of the following
(session readOneOf: Person where: [ :each | each firstName = 'Alan' ]) addresses.
]]]

Before continuing and to make code simpler and more readable we'll
create a few convenience methods:

[[[caption=Constructor method for the InvoiceItem
InvoiceItem class >> description: aString price: aNumber
	^ self new
		description: aString;
		price: aNumber;
		yourself
]]]
[[[caption=To minimize external manipulation of a private collection
Invoice >> addItem: anInvoiceItem
  anInvoiceItem invoice: self.
  self items add: anInvoiceItem
]]]

[[[caption=Returns the added up price of its items
Invoice >> totalPrice
	^ self items sum: #price
]]]

We can now procceed to create an few instances of Invoice, let's create
one Invoice for each person in the database with two items describing
donations to the *Pharo Consortium>http://consortium.pharo.org* and
*Association>http://association.pharo.org* with randomized amounts
(whitin certain range) for each one.

[[[caption=Creating random amount donations
session inUnitOfWorkDo: [
	(session read: Person) do: [:person |
		| invoice |
		invoice := Invoice new
					issueDate: Date today;
					person: person;
					address: person addresses atRandom.
		invoice
			addItem: (InvoiceItem
          description: 'Pharo Consortium donation'
          price: (1000s to: 4000s by: 1000s) atRandom);
			addItem: (InvoiceItem
          description: 'Pharo Association donation'
          price: (20s to: 100s by: 10s) atRandom);
			yourself.
		session register: invoice.
	]
].
]]].

As usual, you can read the Invoices by doing ==session read: Invoice==, let's
print how much each Person donated.

[[[
(session read: Invoice) do: [:each |
  Transcript
    show: each person firstName, ' ', each person lastName;
    show: ': ', each totalPrice printString;
    cr.
]
]]]

When we presented the ReferenceMappings we mentioned how Glorp puts proxies
in the place of a referenced object to avoid performing unnecesary roundtrips
to the database.
In the example above, assuming all caches are empty, Glorp will perform one
query to retrieve all the Invoices and then one extra query to retrieve
the data to instantiate each Person. So if you retrieve Invoices for three
different persons, Glorp will perform four queries, it is four roundtrips,
to the database. Keep that in mind while we continue with other examples.

[[[caption=Calculating the total amount donated
(session read: Invoice) sum: #totalPrice
]]]

The #sum: above will retrieve all the rows from the tables,
instantiate an equivalent quantity of Invoice and then go over all
the invoices' items to sum them. In our example, we only have a few instances
but what if we had a million of them? Instantiating everything at the Pharo
side wouldn't be convenient for such a simple sum. Performing an SQL ==SUM()==
at the database side would be more efficient, since we would only retrieve
the total instead. But how to do such query?

!!! The Query object
So far we've been querying objects by means of sending ==read:== or
==read:where:== to the session object, but if you want to perform
a particular query, like limiting the number of results, running an aggregate
function like ==SUM()==, ==AVG()==, ordering the rows at the server side,
performing subqueries and many other features then the session object
can't provide you with an API for that.

Everyime you sent a ==read:== message to the session, the session created a
simple ==Query== object to read the class passed as parameter, but you can
instantiate a Query object independently, configure it to fit your needs and
execute it in the session. Enter the ==Query== object.

[[[
(session read: Invoice) executeIn: session
]]]

Let's say we want to know the sum of all the prices of our InvoiceItems,
we can do that by using the ==retrieve:== method and using an aggregate function
like ==sum==.

[[[
((SimpleQuery read: InvoiceItem)
  readsOneObject: true;
  retrieve: [:each | each price sum ]) executeIn: session.
]]]

The retrieve method configures the query to return only the retrieved attributes
of the read object, they query will return a collection with the objects
retrieved instead of a collection of instances of the read class.

If we know the beforehand that our query will return an single row with a single
field, it is, a single object, then we can configure the query to return such
object directly by setting ==readsOneObject:== to true.

If you retrieve more than one object then the query will return a collection
of collections, in the form of ''#(#(...) #(...) ...)''.

[[[
((SimpleQuery read: Person)
  retrieve: [:each | each firstName ];
  retrieve: [:each | each lastName]) executeIn: session.
]]]

!!! Attributes of queries
The Query object models an SQL ==SELECT== statement, and like that statement
it can include a ==WHERE==, ===ORDER BY==, ==GROUP BY==, ==HAVING==, etc.

!!!! ''WHERE'' expression
You can always include a ''where'' expression as you did when reading
through the session object, but you can also instantiate a Query inside
of a ''where'' expression to perform a sub-query.
[[[
((SimpleQuery read: Invoice)
  where: [:each | each ]
]]]









((SimpleQuery read: Invoice) retrieve: [:each | each countStar ] ) executeIn: session
